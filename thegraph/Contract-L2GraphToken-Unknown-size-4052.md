
 ------------ ## *MAIN TARGET CONTRACT* TO REVIEW

// SPDX-License-Identifier: GPL-2.0-or-later

pragma solidity ^0.7.6;

// TODO: Re-enable and fix issues when publishing a new version
// solhint-disable gas-indexed-events

import { GraphTokenUpgradeable } from "./GraphTokenUpgradeable.sol";
import { IArbToken } from "@graphprotocol/interfaces/contracts/contracts/arbitrum/IArbToken.sol";

/**
 * @title L2 Graph Token Contract
 * @author Edge & Node
 * @notice Provides the L2 version of the GRT token, meant to be minted/burned
 * through the L2GraphTokenGateway.
 */
contract L2GraphToken is GraphTokenUpgradeable, IArbToken {
    /// @notice Address of the gateway (on L2) that is allowed to mint tokens
    address public gateway;
    /// @notice Address of the corresponding Graph Token contract on L1
    address public override l1Address;

    /**
     * @notice Emitted when the bridge / gateway has minted new tokens, i.e. tokens were transferred to L2
     * @param account Address that received the minted tokens
     * @param amount Amount of tokens minted
     */
    event BridgeMinted(address indexed account, uint256 amount);

    /**
     * @notice Emitted when the bridge / gateway has burned tokens, i.e. tokens were transferred back to L1
     * @param account Address from which tokens were burned
     * @param amount Amount of tokens burned
     */
    event BridgeBurned(address indexed account, uint256 amount);

    /**
     * @notice Emitted when the address of the gateway has been updated
     * @param gateway Address of the new gateway
     */
    event GatewaySet(address gateway);

    /**
     * @notice Emitted when the address of the Graph Token contract on L1 has been updated
     * @param l1Address Address of the L1 Graph Token contract
     */
    event L1AddressSet(address l1Address);

    /**
     * @dev Checks that the sender is the L2 gateway from the L1/L2 token bridge
     */
    modifier onlyGateway() {
        require(msg.sender == gateway, "NOT_GATEWAY");
        _;
    }

    /**
     * @notice L2 Graph Token Contract initializer.
     * @dev Note some parameters have to be set separately as they are generally
     * not expected to be available at initialization time:
     * - gateway using setGateway
     * - l1Address using setL1Address
     * @param _owner Governance address that owns this contract
     */
    function initialize(address _owner) external onlyImpl initializer {
        require(_owner != address(0), "Owner must be set");
        // Initial supply hard coded to 0 as tokens are only supposed
        // to be minted through the bridge.
        GraphTokenUpgradeable._initialize(_owner, 0);
    }

    /**
     * @notice Sets the address of the L2 gateway allowed to mint tokens
     * @param _gw Address for the L2GraphTokenGateway that will be allowed to mint tokens
     */
    function setGateway(address _gw) external onlyGovernor {
        require(_gw != address(0), "INVALID_GATEWAY");
        gateway = _gw;
        emit GatewaySet(_gw);
    }

    /**
     * @notice Sets the address of the counterpart token on L1
     * @param _addr Address for the GraphToken contract on L1
     */
    function setL1Address(address _addr) external onlyGovernor {
        require(_addr != address(0), "INVALID_L1_ADDRESS");
        l1Address = _addr;
        emit L1AddressSet(_addr);
    }

    /**
     * @inheritdoc IArbToken
     * @dev Only callable by the L2GraphTokenGateway when tokens are transferred to L2
     */
    function bridgeMint(address _account, uint256 _amount) external override onlyGateway {
        _mint(_account, _amount);
        emit BridgeMinted(_account, _amount);
    }

    /**
     * @inheritdoc IArbToken
     * @dev Only callable by the L2GraphTokenGateway when tokens are transferred back to L1
     */
    function bridgeBurn(address _account, uint256 _amount) external override onlyGateway {
        burnFrom(_account, _amount);
        emit BridgeBurned(_account, _amount);
    }
}
 ------------
 ------------ END OF MAIN TARGET CONTRACT ------------ 

 ------------ ## SUPPORTING CONTEXT: CONTRACTS, LIBRARIES & INTERFACES ------------ 
// SPDX-License-Identifier: GPL-2.0-or-later

pragma solidity ^0.7.6;

// TODO: Re-enable and fix issues when publishing a new version
// solhint-disable gas-increment-by-one, gas-small-strings, gas-strict-inequalities
// solhint-disable named-parameters-mapping

import { ERC20BurnableUpgradeable } from "@openzeppelin/contracts-upgradeable/token/ERC20/ERC20BurnableUpgradeable.sol";
import { ECDSAUpgradeable } from "@openzeppelin/contracts-upgradeable/cryptography/ECDSAUpgradeable.sol";

import { GraphUpgradeable } from "../../upgrades/GraphUpgradeable.sol";
import { Governed } from "../../governance/Governed.sol";

/**
 * @title GraphTokenUpgradeable contract
 * @author Edge & Node
 * @notice This is the implementation of the ERC20 Graph Token.
 * The implementation exposes a permit() function to allow for a spender to send a signed message
 * and approve funds to a spender following EIP2612 to make integration with other contracts easier.
 *
 * The token is initially owned by the deployer address that can mint tokens to create the initial
 * distribution. For convenience, an initial supply can be passed in the constructor that will be
 * assigned to the deployer.
 *
 * The governor can add contracts allowed to mint indexing rewards.
 *
 * Note this is an exact copy of the original GraphToken contract, but using
 * initializer functions and upgradeable OpenZeppelin contracts instead of
 * the original's constructor + non-upgradeable approach.
 */
abstract contract GraphTokenUpgradeable is GraphUpgradeable, Governed, ERC20BurnableUpgradeable {
    // -- EIP712 --
    // https://github.com/ethereum/EIPs/blob/master/EIPS/eip-712.md#definition-of-domainseparator

    /// @dev Hash of the EIP-712 Domain type
    bytes32 private immutable DOMAIN_TYPE_HASH =
        keccak256("EIP712Domain(string name,string version,uint256 chainId,address verifyingContract,bytes32 salt)");
    /// @dev Hash of the EIP-712 Domain name
    bytes32 private immutable DOMAIN_NAME_HASH = keccak256("Graph Token");
    /// @dev Hash of the EIP-712 Domain version
    bytes32 private immutable DOMAIN_VERSION_HASH = keccak256("0");
    /// @dev EIP-712 Domain salt
    bytes32 private immutable DOMAIN_SALT = 0xe33842a7acd1d5a1d28f25a931703e5605152dc48d64dc4716efdae1f5659591; // Randomly generated salt
    /// @dev Hash of the EIP-712 permit type
    bytes32 private immutable PERMIT_TYPEHASH =
        keccak256("Permit(address owner,address spender,uint256 value,uint256 nonce,uint256 deadline)");

    // -- State --

    /// @dev EIP-712 Domain separator
    bytes32 private DOMAIN_SEPARATOR; // solhint-disable-line var-name-mixedcase
    /// @dev Addresses for which this mapping is true are allowed to mint tokens
    mapping(address => bool) private _minters;
    /// @notice Nonces for permit signatures for each token holder
    mapping(address => uint256) public nonces;
    /// @dev Storage gap added in case we need to add state variables to this contract
    uint256[47] private __gap;

    // -- Events --

    /**
     * @notice Emitted when a new minter is added
     * @param account Address of the minter that was added
     */
    event MinterAdded(address indexed account);

    /**
     * @notice Emitted when a minter is removed
     * @param account Address of the minter that was removed
     */
    event MinterRemoved(address indexed account);

    /// @dev Reverts if the caller is not an authorized minter
    modifier onlyMinter() {
        require(isMinter(msg.sender), "Only minter can call");
        _;
    }

    /**
     * @notice Approve token allowance by validating a message signed by the holder.
     * @param _owner Address of the token holder
     * @param _spender Address of the approved spender
     * @param _value Amount of tokens to approve the spender
     * @param _deadline Expiration time of the signed permit (if zero, the permit will never expire, so use with caution)
     * @param _v Signature recovery id
     * @param _r Signature r value
     * @param _s Signature s value
     */
    function permit(
        address _owner,
        address _spender,
        uint256 _value,
        uint256 _deadline,
        uint8 _v,
        bytes32 _r,
        bytes32 _s
    ) external {
        require(_deadline == 0 || block.timestamp <= _deadline, "GRT: expired permit");
        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                DOMAIN_SEPARATOR,
                keccak256(abi.encode(PERMIT_TYPEHASH, _owner, _spender, _value, nonces[_owner], _deadline))
            )
        );

        address recoveredAddress = ECDSAUpgradeable.recover(digest, _v, _r, _s);
        require(_owner == recoveredAddress, "GRT: invalid permit");

        nonces[_owner] = nonces[_owner] + 1;
        _approve(_owner, _spender, _value);
    }

    /**
     * @notice Add a new minter.
     * @param _account Address of the minter
     */
    function addMinter(address _account) external onlyGovernor {
        require(_account != address(0), "INVALID_MINTER");
        _addMinter(_account);
    }

    /**
     * @notice Remove a minter.
     * @param _account Address of the minter
     */
    function removeMinter(address _account) external onlyGovernor {
        require(isMinter(_account), "NOT_A_MINTER");
        _removeMinter(_account);
    }

    /**
     * @notice Renounce being a minter.
     */
    function renounceMinter() external {
        require(isMinter(msg.sender), "NOT_A_MINTER");
        _removeMinter(msg.sender);
    }

    /**
     * @notice Mint new tokens.
     * @param _to Address to send the newly minted tokens
     * @param _amount Amount of tokens to mint
     */
    function mint(address _to, uint256 _amount) external onlyMinter {
        _mint(_to, _amount);
    }

    /**
     * @notice Return if the `_account` is a minter or not.
     * @param _account Address to check
     * @return True if the `_account` is minter
     */
    function isMinter(address _account) public view returns (bool) {
        return _minters[_account];
    }

    /**
     * @notice Graph Token Contract initializer.
     * @param _owner Owner of this contract, who will hold the initial supply and will be a minter
     * @param _initialSupply Initial supply of GRT
     */
    function _initialize(address _owner, uint256 _initialSupply) internal {
        __ERC20_init("Graph Token", "GRT");
        Governed._initialize(_owner);

        // The Governor has the initial supply of tokens
        _mint(_owner, _initialSupply);

        // The Governor is the default minter
        _addMinter(_owner);

        // EIP-712 domain separator
        DOMAIN_SEPARATOR = keccak256(
            abi.encode(
                DOMAIN_TYPE_HASH,
                DOMAIN_NAME_HASH,
                DOMAIN_VERSION_HASH,
                _getChainID(),
                address(this),
                DOMAIN_SALT
            )
        );
    }

    /**
     * @notice Add a new minter.
     * @param _account Address of the minter
     */
    function _addMinter(address _account) private {
        _minters[_account] = true;
        emit MinterAdded(_account);
    }

    /**
     * @notice Remove a minter.
     * @param _account Address of the minter
     */
    function _removeMinter(address _account) private {
        _minters[_account] = false;
        emit MinterRemoved(_account);
    }

    /**
     * @notice Get the running network chain ID.
     * @return The chain ID
     */
    function _getChainID() private pure returns (uint256) {
        uint256 id;
        // solhint-disable-next-line no-inline-assembly
        assembly {
            id := chainid()
        }
        return id;
    }
}

// SPDX-License-Identifier: GPL-2.0-or-later

pragma solidity ^0.7.6 || 0.8.27 || 0.8.33;

/* solhint-disable gas-custom-errors */ // Cannot use custom errors with 0.7.6

/**
 * @title Graph Governance contract
 * @author Edge & Node
 * @notice All contracts that will be owned by a Governor entity should extend this contract.
 */
abstract contract Governed {
    // -- State --

    /**
     * @notice Address of the governor
     */
    address public governor;
    /**
     * @notice Address of the new governor that is pending acceptance
     */
    address public pendingGovernor;

    // -- Events --

    /**
     * @notice Emitted when a new owner/governor has been set, but is pending acceptance
     * @param from Previous pending governor address
     * @param to New pending governor address
     */
    event NewPendingOwnership(address indexed from, address indexed to);

    /**
     * @notice Emitted when a new owner/governor has accepted their role
     * @param from Previous governor address
     * @param to New governor address
     */
    event NewOwnership(address indexed from, address indexed to);

    /**
     * @dev Check if the caller is the governor.
     */
    modifier onlyGovernor() {
        require(msg.sender == governor, "Only Governor can call");
        _;
    }

    /**
     * @notice Initialize the governor for this contract
     * @param _initGovernor Address of the governor
     */
    function _initialize(address _initGovernor) internal {
        governor = _initGovernor;
    }

    /**
     * @notice Admin function to begin change of governor. The `_newGovernor` must call
     * `acceptOwnership` to finalize the transfer.
     * @param _newGovernor Address of new `governor`
     */
    function transferOwnership(address _newGovernor) external onlyGovernor {
        require(_newGovernor != address(0), "Governor must be set");

        address oldPendingGovernor = pendingGovernor;
        pendingGovernor = _newGovernor;

        emit NewPendingOwnership(oldPendingGovernor, pendingGovernor);
    }

    /**
     * @notice Admin function for pending governor to accept role and update governor.
     * This function must called by the pending governor.
     */
    function acceptOwnership() external {
        address oldPendingGovernor = pendingGovernor;

        require(
            oldPendingGovernor != address(0) && msg.sender == oldPendingGovernor,
            "Caller must be pending governor"
        );

        address oldGovernor = governor;

        governor = oldPendingGovernor;
        pendingGovernor = address(0);

        emit NewOwnership(oldGovernor, governor);
        emit NewPendingOwnership(oldPendingGovernor, pendingGovernor);
    }
}

// SPDX-License-Identifier: GPL-2.0-or-later

pragma solidity ^0.7.6 || 0.8.27 || 0.8.33;

/* solhint-disable gas-custom-errors */ // Cannot use custom errors with 0.7.6

import { IGraphProxy } from "@graphprotocol/interfaces/contracts/contracts/upgrades/IGraphProxy.sol";

/**
 * @title Graph Upgradeable
 * @author Edge & Node
 * @notice This contract is intended to be inherited from upgradeable contracts.
 */
abstract contract GraphUpgradeable {
    /**
     * @dev Storage slot with the address of the current implementation.
     * This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

    /**
     * @dev Check if the caller is the proxy admin.
     * @param _proxy The proxy contract to check admin for
     */
    modifier onlyProxyAdmin(IGraphProxy _proxy) {
        require(msg.sender == _proxy.admin(), "Caller must be the proxy admin");
        _;
    }

    /**
     * @dev Check if the caller is the implementation.
     */
    modifier onlyImpl() {
        require(msg.sender == _implementation(), "Only implementation");
        _;
    }

    /**
     * @notice Returns the current implementation.
     * @return impl Address of the current implementation
     */
    function _implementation() internal view returns (address impl) {
        bytes32 slot = IMPLEMENTATION_SLOT;
        // solhint-disable-next-line no-inline-assembly
        assembly {
            impl := sload(slot)
        }
    }

    /**
     * @notice Accept to be an implementation of proxy.
     * @param _proxy Proxy to accept
     */
    function acceptProxy(IGraphProxy _proxy) external onlyProxyAdmin(_proxy) {
        _proxy.acceptUpgrade();
    }

    /**
     * @notice Accept to be an implementation of proxy and then call a function from the new
     * implementation as specified by `_data`, which should be an encoded function call. This is
     * useful to initialize new storage variables in the proxied contract.
     * @param _proxy Proxy to accept
     * @param _data Calldata for the initialization function call (including selector)
     */
    function acceptProxyAndCall(IGraphProxy _proxy, bytes calldata _data) external onlyProxyAdmin(_proxy) {
        _proxy.acceptUpgradeAndCall(_data);
    }
}


## ------------ SUPPORTING CONTEXT: INTERFACES AND ROOT IMPLEMENTATIONS ------------ 

## ------------ SUPPORTING CONTEXT: EXTERNAL LIBRARIES ------------ 

 ------------ END OF SUPPORTING CONTRACTS AND INTERFACES ------------ 


 ------------ ## DEPLOYMENT SCRIPTS ------------ 

